import type_options
/*
    This file for the log statments that relates to each diffrent applications
*/

SpecialCommand:
  writer_command | calc_command | impress_command | math_command
;

/*
  This part for all the writer log statments:
   
    1)  Type 
    2)  Select
    3)  GOTO page

  then we can add whatever we need in the future
*/
writer_command:
    writer_Type_command | writer_Select_command | writer_GOTO_command |
    writer_Create_table 
;
writer_Type_command:
  'Type on writer' what_to_type=Type_options
;
writer_Select_command:
  'Select from Pos' from_pos=INT 'to Pos' to_pos=INT 
;
writer_GOTO_command:
  'GOTO page number' page_num=INT
;
writer_Create_table:
  'Create Table with  Columns :' cols=INT ', Rows :' rows=INT
;
//=================================================================//
/*
  This part for all the calc log statments:

    1)  select sheet
    2)  Select cell or range
    3)  launch AutoFill

  then we can add whatever we need in the future
*/
calc_command:
  calc_Type_command | calc_switch_sheet | calc_Select_cell | calc_AutoFill_filter |
  calc_Delete_Cells | calc_Remove_Content | calc_insert_cells | calc_Cut_Cells | 
  calc_Copy_Cells | calc_Paste_Cells | calc_UNMerge_Cells | calc_Merge_Cells
;
calc_Type_command:
  'Type on current cell' what_to_type=Type_options
;
calc_switch_sheet:
  'Switch to sheet number' sheet_num=INT
;
calc_Select_cell:
  'Select from calc' select_op=select_options
;
calc_AutoFill_filter:
  'Lanuch AutoFilter from Col' col_num=INT 'and Row' row_num=INT
;
//this is the select options
select_options:
    one_cell | range_of_cells
;
range_of_cells:
    '{' '"RANGE":' input_range=STRING '}'
;
one_cell:
    '{' '"CELL":' input_cell=STRING '}'
;
calc_Delete_Cells:
    'Delete The Cells in' '{' '"RANGE":' input_range=STRING '}'
;
calc_Remove_Content:
    'Remove Content from This' '{' '"RANGE":' input_range=STRING '}'
;
calc_insert_cells:
    'Insert Cell around the ' '{' '"RANGE":' input_range=STRING '}'
;
calc_Cut_Cells:
    'CUT the selected ' '{' '"RANGE":' input_range=STRING '}'
;
calc_Copy_Cells:
    'COPY the selected ' '{' '"RANGE":' input_range=STRING '}'
;
calc_Paste_Cells:
    'Paste in the' '{' '"RANGE":' input_range=STRING '}'
;
calc_Merge_Cells:
    'Merge' '{' '"RANGE":' input_range=STRING '}'
;
calc_UNMerge_Cells:
    'Delete the merge between' '{' '"CELL":' input_range=STRING '}'
;
//=================================================================//
/*
  This part for all the impress log statments:
   
    1)  Type 
    3)  Set zoom

  then we can add whatever we need in the future
*/
impress_command:
    impress_Type_command 
;
impress_Type_command:
    'Type on impress ' what_to_type=Type_options
;
//=================================================================//
/*
  This part for all the math log statments:
   
    1)  element selector 
    2) Type

  then we can add whatever we need in the future
*/
math_command:
    math_element_selector | math_Type_command
;
math_element_selector:
    'Select element no ' element_no=INT ' From ' place=ID
;
math_Type_command:
    'Type on math ' what_to_type=Type_options
;



